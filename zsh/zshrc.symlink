#! /usr/bin/env zsh

################################################################################
# How Low Can You Go?
export DOTFILES="$HOME/.dotfiles"
export EDITOR='/usr/local/bin/nvim'
bindkey -v # VIM tho

################################################################################
# All the ZSH files.
typeset -U config_files
config_files=($DOTFILES/*/*.zsh)

################################################################################
# load the env files
for file in ${(M)config_files:#*/env.zsh}; do
  source "$file"
done

################################################################################
# Load Antibody Plugins
source ~/.zsh_plugins.sh

################################################################################
# Load everything but the env files.
for file in ${config_files:#*/env.zsh}; do
  source "$file"
done

################################################################################
# Initialize the autocompletion framework.
autoload -Uz compinit
typeset -i updated_at=$(date +'%j' -r ~/.zcompdump 2>/dev/null || stat -f '%Sm' -t '%j' ~/.zcompdump 2>/dev/null)
if [ $(date +'%j') != $updated_at ]; then
  compinit -i
else
  compinit -C -i
fi

################################################################################
unset config_files updated_at

################################################################################
# Load local configurations, including $PRIVATE_DOTFILES.
[ -f ${HOME}/.zsh.local ] && . ${HOME}/.zsh.local
